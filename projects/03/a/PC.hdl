// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Increment counter and provide output
    Inc16(in=feedback, out=addOneOut);
    Mux16(a=feedback, b=addOneOut, sel=inc, out=incOut);

    // Load output
    Mux16(a=incOut, b=in, sel=load, out=setOut);

    // Reset output
    Mux16(a=setOut, b=false, sel=reset, out=resetOut);

    Register(in=resetOut, load=true, out=out, out=feedback);
}
